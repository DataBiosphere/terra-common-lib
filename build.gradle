plugins {
    id 'idea'
    id 'jacoco'
    id 'java-library'
    id 'maven-publish'

    id 'com.diffplug.spotless' version '6.12.1'
    id 'com.github.ben-manes.versions' version '0.44.0'
    id 'com.github.spotbugs' version '5.0.13'
    id 'com.jfrog.artifactory' version '4.31.0'
    id 'org.springframework.boot' version '2.7.7' // 3.* versions require jakarta support in dependencies
    id 'ru.vyarus.quality' version '4.8.0'

    // [dd 2021-05-17] Use of Spring dependency management generates invalid POM, making
    // it impossible to publish to mavenLocal. In order to make TCL testable, I have
    // removed this plugin and filled in specific version numbers. I also removed all
    // version numbers with `+` in them. Was:
    //     id 'io.spring.dependency-management' version '1.0.10.RELEASE'
}

group 'bio.terra'
version '0.0.78-local-testing'

// If true, search local repository (~/.m2/repository/) first for dependencies.
def useMavenLocal = false
repositories {
    if (useMavenLocal) {
        mavenLocal() // must be listed first to take effect
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release-local/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

// for scans
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

project.ext {
    includeDir = "$projectDir/gradle"
}

apply(from: "$includeDir/application.gradle")
apply(from: "$includeDir/dependencies.gradle")
apply(from: "$includeDir/dependency-locking.gradle")
apply(from: "$includeDir/jacoco.gradle")
apply(from: "$includeDir/javadoc.gradle")
apply(from: "$includeDir/publishing.gradle")
apply(from: "$includeDir/quality.gradle")
apply(from: "$includeDir/spotless.gradle")
apply(from: "$includeDir/taskDependencies.gradle")
apply(from: "$includeDir/test.gradle")
