plugins {
    id 'idea'
    id 'jacoco'
    id 'java-library'
    id 'maven-publish'

    id 'com.diffplug.spotless' version '6.10.0'
    id 'com.github.spotbugs' version '5.0.12'
    id 'com.github.ben-manes.versions' version '0.42.0'
    id 'com.jfrog.artifactory' version '4.28.2'
    // [dd 2021-05-17] Use of Spring dependency management generates invalid POM, making
    // it impossible to publish to mavenLocal. In order to make TCL testable, I have
    // removed this plugin and filled in specific version numbers. I also removed all
    // version numbers with `+` in them. Was:
    //     id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    id 'org.springframework.boot' version '2.7.3'
    id 'ru.vyarus.quality' version '4.8.0'
}

// TODO(PF-1981) - Move version to settings.gradle
group 'bio.terra'
version '0.0.68-SNAPSHOT'

// If true, search local repository (~/.m2/repository/) first for dependencies.
def useMavenLocal = false
repositories {
    if (useMavenLocal) {
        mavenLocal() // must be listed first to take effect
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release-local/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

dependencies {
    // Google dependencies
    constraints {
        // "-jre" for Java 8 or higher
        implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
    }
    api group: 'com.google.guava', name: 'guava'

    implementation platform('com.google.cloud:libraries-bom:26.1.1') // use common bom
    implementation group: 'com.google.cloud', name: 'google-cloud-core'
    implementation group: 'com.google.cloud', name: 'google-cloud-pubsub'
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.21.6'

    // static analysis
    compileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.7.2'
    testCompileOnly group: 'com.github.spotbugs', name: 'spotbugs-annotations', version: '4.7.2'

    implementation group: 'com.google.apis', name: 'google-api-services-logging', version: 'v2-rev20220819-2.0.0'

    // Publish the stairway library here to minimize rollout overhead of new Stairways
    api group: 'bio.terra', name: 'stairway-gcp', version: '0.0.71-SNAPSHOT'

    implementation group: 'org.broadinstitute.dsde.workbench', name: 'sam-client_2.13', version: '0.1-fde559d'

    // commons-dbcp2 and -lang3 versions are managed by the Spring Boot plugin
    implementation group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.9.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'org.apache.commons', name: 'commons-pool2', version: '2.11.1'
    testImplementation group: 'org.apache.commons', name: 'commons-dbcp2', version: '2.9.0'

    implementation group: 'org.hashids', name: 'hashids', version: '1.0.3'
    implementation group: 'org.liquibase' , name: 'liquibase-core', version: '4.16.0'
    implementation group: 'org.postgresql', name: 'postgresql', version: '42.5.0'
    implementation group: 'org.slf4j', name: 'slf4j-api', version: '1.7.36'

    implementation group: 'org.springframework' , name: 'spring-context', version: '5.3.23'
    implementation group: 'org.springframework' , name: 'spring-core', version: '5.3.23'
    implementation group: 'org.springframework.retry', name: 'spring-retry', version: '1.3.3'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-data-jdbc', version: '2.7.3'
    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: '2.7.3'

    implementation group: 'ch.qos.logback.contrib', name: 'logback-json-classic', version: '0.1.5'
    implementation group: 'ch.qos.logback.contrib', name: 'logback-jackson', version: '0.1.5'
    implementation group: 'io.kubernetes', name: 'client-java', version: '16.0.0'
    implementation group: 'jakarta.ws.rs', name: 'jakarta.ws.rs-api', version: '2.1.6'

    ext {
        opencensus = '0.31.1'
    }

    implementation group: 'io.opencensus', name: 'opencensus-exporter-stats-prometheus', version: opencensus
    implementation group: 'io.opencensus', name: 'opencensus-exporter-trace-stackdriver', version: opencensus
    runtimeOnly group: 'io.opencensus', name: 'opencensus-impl', version: opencensus
    api group: 'io.opencensus', name: 'opencensus-api', version: opencensus
    api group: 'io.opencensus', name: 'opencensus-contrib-spring', version: opencensus
    testImplementation group: 'io.opencensus', name: 'opencensus-impl', version: opencensus

    implementation group: 'io.prometheus', name: 'simpleclient_httpserver', version: '0.16.0'

    // Jsonpath, Jupiter versions is managed by the Spring Boot plugin
    testImplementation('org.springframework.boot:spring-boot-starter-test:2.7.3') {
        exclude group: 'com.vaadin.external.google', module: 'android-json'
    }
    testImplementation group: 'com.jayway.jsonpath', name: 'json-path', version: '2.7.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.9.0'
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.9.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.8.0'
    testImplementation group: 'org.openapitools', name: 'jackson-databind-nullable', version: '0.2.3'

    annotationProcessor group: 'org.springframework.boot', name: 'spring-boot-configuration-processor', version: '2.7.3'
}

// for scans
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

apply from: "$rootDir/gradle/application.gradle"
apply from: "$rootDir/gradle/dependency-locking.gradle"
apply from: "$rootDir/gradle/javadoc.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/quality.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/test.gradle"
